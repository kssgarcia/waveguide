      subroutine arc_ax 
     +
     +   (Iarc
     +   ,X1,X2,X3
     +   ,Y1,Y2,Y3
     +   ,XC,YC
     +   ,R
     +   ,TH1,TH2,TH3,ORNT
     +   ,AREA1,AREA2,AREA
     +   ,VOL1,VOL2,VOL
     +   ,XCN1,XCN2,XCN
     +   ,YCN1,YCN2,YCN
     +   ,Istop
     +   )

c------------------------------------------
c Copyright by C. Pozrikidis, 1999
c All rights reserved.
c
c This program is to be used only under the
c stipulations of the licensing agreement.
c-----------------------------------------

c------------------------------------------------------------
c
c  Properties of the circular arc passing through
c  three points numbered 1,2,3
c
c  The arc represents the trace of an axisymmetric surface
c  in the xy plane
c
c  The surface is generated by revolving the line around
c  the x axis
c
c  LEGEND:
c  ------
c
c  (xc,yc)      arc center
c
c  th1, th2, th3  angles subtended by the arc center and
c                 each one of the points
c
c  ornt         arc orientation: 1 for counterclockwise
c                               -1 for clockwise
c
c  area1        area between the points 12 and the x-axis
c  area2        area between the points 23 and the x-axis
c  area         area1+area2
c
c------------------------------------------------------------

      Implicit Double Precision (a-h,o-z)

      Parameter (eps=0.0000000001,tol=0.0000000001)
      Parameter (tol1=0.000001)
 
c----------
c constants
c----------

      pi  = 3.1415 92653 58979 32384 D0
      pi2 = 2.0D0*pi

      epsm = - eps

c----------------
c arc orientation
c----------------

      ORNT = (X2-X1)*(Y3-Y1)-(X3-X1)*(Y2-Y1)
      D1   = SQRT((X2-X1)**2+(Y2-Y1)**2)
      D2   = SQRT((X3-X1)**2+(Y3-Y1)**2)
      ORNT = ORNT/(D1*D2)

      If(ORNT.GT.eps) then
        ORNT= 1.0
      Else If(ORNT.LT.epsm) then
        ORNT=-1.0
      Else
        write (6,*)
        write (6,*) ' arc_ax: arc ',Iarc,' is not defined'
        write (6,*)
        Istop = 1
        Go to 99
      End If
 
c-----------
c arc center
c-----------

      A1=(Y2-Y1)/(X2-X1)
      A2=(Y3-Y2)/(X3-X2)
 
      If(ABS(A1).LE.tol) A1 = tol
      If(ABS(A2).LE.tol) A2 = tol
 
      AA1 = -1.0D0/A1
      AA2 = -1.0D0/A2
 
      XX1 = 0.5D0*(X1+X2)
      YY1 = 0.5D0*(Y1+Y2)
      XX2 = 0.5D0*(X2+X3)
      YY2 = 0.5D0*(Y2+Y3)
 
      GG1 = YY1-AA1*XX1
      GG2 = YY2-AA2*XX2
 
      XC = (GG1-GG2)/(AA2-AA1)
      YC = AA1*XC+GG1

c-----------
c arc radius
c-----------

      R2 = (X1-XC)*(X1-XC)+(Y1-YC)*(Y1-YC)
      R  = Dsqrt(R2)

c-------
c angles
c-------

      ARG = (X1-XC)/R

      If(ARG.GT.1.0) then
        write (6,300) ARG
        ARG= 1.0
      Else If (ARG.LT.-1.0)  then
        write (6,*) ARG
        ARG=-1.0
      End If

      TH1  =  ACOS(ARG)
      SINA =  Y1-YC
      IF(SINA.LT.-0.000001) TH1=PI2-TH1
      PROD2 = (X1-XC)*(X2-XC)+(Y1-YC)*(Y2-YC)
      PROD3 = (X1-XC)*(X3-XC)+(Y1-YC)*(Y3-YC)

      DTH2 = ORNT*ACOS(PROD2/R2)
      DTH3 = ORNT*ACOS(PROD3/R2)

      TH2  = TH1+DTH2
      TH3  = TH1+DTH3

c-------------------------
c Compute integrals:
c
c Int (y**2 dx) = Int( y dA) = Int(dV)
c
c-------------------------

      COS1  = COS(TH1)
      COS2  = COS(TH2)
      COS3  = COS(TH3)
      SIN1  = SIN(TH1)
      SIN2  = SIN(TH2)
      SIN3  = SIN(TH3)

c---
c first part of the arc
c---

      DTH   = TH2           -TH1
      DCS   = COS2          -COS1
      DCS3  = COS2**3       -COS1**3
      DSC31 = SIN2**3*COS2  -SIN1**3*COS1
      DSN   = SIN2          -SIN1
      DSN2  = SIN2**2       -SIN1**2
      DSN3  = SIN2**3       -SIN1**3
      DSN4  = SIN2**4       -SIN1**4
      DSIN2 = DSIN(2*TH2)   -DSIN(2*TH1)
 
      AREA1 = ORNT*PI2*R*(YC*DTH-R*DCS)

      VOL1  = PI*R*(  YC*YC *(-DCS)+
     +              2*YC*R  *(0.50*DTH-0.25*DSIN2)+
     +                R2    *(-DCS+DCS3/3.0))

      XCN1 =    XC*YC*YC    *(-DCS)+
     +        2*XC*YC*R     *(0.50*DTH-0.25*DSIN2)+
     +          XC*R *R     *(-DCS+DCS3/3.0)+
     +          R *YC*YC    *DSN2/2.0+
     +        2*R *R *YC    *DSN3/3.0+
     +          R *R *R     *DSN4/4.0 
      XCN1 = XCN1*R*pi

      YCN1 =   YC*YC*YC     *(-DCS)+
     +       3*YC*YC*R      *(0.50*DTH-0.25*DSIN2)+
     +       3*YC*R *R      *(-DCS+DCS3/3.0)+
     +         R *R *R      *(3*DTH/8.0-3*DSIN2/16-0.25*DSC31)
      YCN1 = YCN1*R*pi

c---
c second part of the arc
c---

      DTH   = TH3          -TH2
      DCS   = COS3         -COS2
      DCS3  = COS3**3      -COS2**3
      DSC31 = SIN3**3*COS3 -SIN2**3*COS2
      DSN   = SIN3         -SIN2
      DSN2  = SIN3**2      -SIN2**2
      DSN3  = SIN3**3      -SIN2**3
      DSN4  = SIN3**4      -SIN2**4
      DSIN2 = DSIN(2*TH3)  -DSIN(2*TH2)
 
      AREA2 = ORNT*PI2*R*(YC*DTH-R*DCS)
      VOL2  = PI*R*(  YC*YC *(-DCS)+
     +              2*YC*R  *(0.50*DTH-0.25*DSIN2)+
     +                R2    *(-DCS+DCS3/3.0))
 
      XCN2 =    XC*YC*YC    *(-DCS)+
     +        2*XC*YC*R     *(0.50*DTH-0.25*DSIN2)+
     +          XC*R *R     *(-DCS+DCS3/3.0)+
     +          R *YC*YC    *DSN2/2.0+
     +        2*R *R *YC    *DSN3/3.0+
     +          R *R *R     *DSN4/4.0
      XCN2 = XCN2*R*pi

      YCN2 =   YC*YC*YC     *(-DCS)+
     +       3*YC*YC*R      *(0.50*DTH-0.25*DSIN2)+
     +       3*YC*R *R      *(-DCS+DCS3/3.0)+
     +         R *R *R      *(3*DTH/8.0-3*DSIN2/16-0.25*DSC31)
      YCN2 = YCN2*R*pi
 
c---
c whole arc
c---

      AREA  = AREA1 + AREA2
      VOL   = VOL1  + VOL2
      XCN   = XCN1  + XCN2
      YCN   = YCN1  + YCN2

c-----------------------
c  CHECK FOR CONSISTENCY
c-----------------------

      XX1 = XC+R*COS(TH1)
      YY1 = YC+R*SIN(TH1)

      XX2 = XC+R*COS(TH2)
      YY2 = YC+R*SIN(TH2)

      XX3 = XC+R*COS(TH3)
      YY3 = YC+R*SIN(TH3)

      D1  = XX1-X1
      D2  = XX2-X2
      D3  = XX3-X3

      F1  = YY1-Y1
      F2  = YY2-Y2
      F3  = YY3-Y3

      If(ABS(D1).GE.tol1) write (6,101) XX1,X1
      If(ABS(D2).GE.tol1) write (6,102) XX2,X2
      If(ABS(D3).GE.tol1) write (6,103) XX3,X3
      If(ABS(F1).GE.tol1) write (6,104) YY1,Y1
      If(ABS(F2).GE.tol1) write (6,105) YY2,Y2
      If(ABS(F3).GE.tol1) write (6,106) YY3,Y3
 
  99  Continue

c-----
c Done
c-----

 100  Format (10(1X,F10.5))
 101  Format (' PROBLEM 1 WITH ARC',F13.9,2X,F13.9)
 102  Format (' PROBLEM 2 WITH ARC',F13.9,2X,F13.9)
 103  Format (' PROBLEM 3 WITH ARC',F13.9,2X,F13.9)
 104  Format (' PROBLEM 4 WITH ARC',F13.9,2X,F13.9)
 105  Format (' PROBLEM 5 WITH ARC',F13.9,2X,F13.9)
 106  Format (' PROBLEM 6 WITH ARC',F13.9,2X,F13.9)

 300  Format (' WARNING IN ARC',/,
     +        ' POINT 1 IS ALLIGNED WITH THE X AXIS',/,
     +        ' DIRECTION COSINE WAS FOUND TO BE ',F15.12,/,
     +        ' WILL SET EQUAL TO +1 OR -1')

      Return
      End
